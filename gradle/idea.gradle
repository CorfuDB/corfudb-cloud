import groovy.xml.XmlUtil

import java.nio.file.Paths

idea {
    project {
        // Set the version control system
        // to Git for this project.
        // All values IntelliJ IDEA supports
        // can be used. E.g. Subversion, Mercurial.
        vcs = 'Git'

        languageLevel = '1.8'
    }

    module {
        downloadJavadoc = true
        downloadSources = false

        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

/**
 * Delegate running builds and tests to gradle.
 * Manual settings: preferences -> Build, Execution, Deployment -> gradle -> [build and run using | Run tests using]
 *
 * These settings make IDEA using Gradle instead of a native builder, which makes it consistent with Gradle build
 * and solves a problem when IDEA messed up with properties files in the resource directory
 */
task ideaGradleRunner {
    doLast {
        File gradleSettings = Paths.get(".idea", "gradle.xml").toFile()

        def xml = new XmlParser().parse(gradleSettings)
        def gradleTag = xml.'*'
                .find { node -> node.@name == 'GradleSettings' }
                .option
                .GradleProjectSettings

        gradleTag.'*'.find { node -> node.@name == 'delegatedBuild' }.@value = true
        gradleTag.'*'.find { node -> node.@name == 'testRunner' }.@value = 'GRADLE'

        gradleSettings.withWriter { outWriter ->
            XmlUtil.serialize(xml, outWriter)
        }
    }
}

/**
 * Whenever processResources executed it also delegates a build task to the gradle
 */
processResources {
    dependsOn("ideaGradleRunner")
}