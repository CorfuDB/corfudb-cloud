import org.apache.commons.io.IOUtils

import java.nio.file.Paths
import java.util.jar.JarEntry
import java.util.jar.JarFile

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'commons-io:commons-io:2.6'
    }
}

configurations {
    corfu
}

dependencies {
    corfu("org.corfudb:infrastructure:${corfuVersion}:shaded") {
        transitive = false
        exclude group: 'io.netty', module: 'netty-tcnative'
    }
}

processResources {
    /**
     * Download infrastructure jar
     */
    doLast {
        copy {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            from configurations.corfu
            into buildDir
        }
    }
}

task corfuInfo {
    doLast {
        File corfuJarFile = file(project.configurations.corfu[0])
        JarFile file = new JarFile(corfuJarFile)

        JarEntry entry = file.getJarEntry("git.properties")
        InputStream gitPropsStream = file.getInputStream(entry)

        String content = IOUtils.toString(gitPropsStream)
        println(content)
    }
}

task setCorfuServerVersion {
    doLast {
        def corfuServerVersion = project.getProperty('corfu.server.version')
        ant.propertyfile(file: 'src/main/resources/universe-tests.properties') {
            entry(key: "server.version", value: corfuServerVersion)
        }
    }
}
